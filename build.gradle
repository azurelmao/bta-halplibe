buildscript {
	ext.kotlin_version = '2.1.0'
	dependencies {
		classpath 'org.kohsuke:github-api:1.135'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
	repositories {
		mavenCentral()
	}
}

plugins {
	id 'fabric-loom' version '1.7.bta'
	id 'java'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.3.4"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
}

group = project.mod_group
archivesBaseName = project.mod_name
version = project.mod_version

loom {
	noIntermediateMappings()
	customMinecraftMetadata.set("https://downloads.betterthanadventure.net/bta-client/${project.bta_channel}/v${project.bta_version}/manifest.json")
	accessWidenerPath = file("src/main/resources/halplibe.accesswidener")
}

repositories {
	mavenCentral()
	maven { url = "https://jitpack.io" }
	maven {
		name = 'Babric'
		url = 'https://maven.glass-launcher.net/babric'
	}
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = 'signalumMaven'
		url = 'https://maven.thesignalumproject.net/infrastructure'
	}
	maven {
		name = 'signalumMaven'
		url = 'https://maven.thesignalumproject.net/releases'
	}
	ivy {
		url = "https://github.com/Better-than-Adventure"
		patternLayout {
			artifact "[organisation]/releases/download/v[revision]/[module].jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://downloads.betterthanadventure.net/bta-client/${project.bta_channel}/"
		patternLayout {
			artifact "/v[revision]/client.jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://downloads.betterthanadventure.net/bta-server/${project.bta_channel}/"
		patternLayout {
			artifact "/v[revision]/server.jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}
	ivy {
		url = "https://piston-data.mojang.com"
		patternLayout {
			artifact "v1/[organisation]/[revision]/[module].jar"
			m2compatible = true
		}
		metadataSources { artifact() }
	}

}

dependencies {
	minecraft "::${project.bta_version}"
	mappings loom.layered() {}

	modRuntimeOnly "objects:client:43db9b498cb67058d2e12d394e6507722e71bb45" // https://piston-data.mojang.com/v1/objects/43db9b498cb67058d2e12d394e6507722e71bb45/client.jar
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation("turniplabs:modmenu-bta:${project.mod_menu_version}")

	implementation "org.slf4j:slf4j-api:1.8.0-beta4"
	implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.16.0"

	implementation 'com.google.guava:guava:33.0.0-jre'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	var log4jVersion = "2.20.0"
	implementation("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-api:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}")

	include(implementation("org.apache.commons:commons-lang3:3.12.0"))

	modImplementation("com.github.zarzelcow:legacy-lwjgl3:1.0.4")
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
	implementation "org.lwjgl:lwjgl:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-assimp:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-openal:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-opengl:$lwjglVersion"
	implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.release.set 8
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

configurations.configureEach {
	// Removes LWJGL2 dependencies
	exclude group: "org.lwjgl.lwjgl"
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

publishing {
	repositories {
		maven {
			name = "signalumMaven"
			url = "https://maven.thesignalumproject.net/releases"
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}

	publications {
		maven(MavenPublication) {
			groupId = project.mod_group
			artifactId = project.mod_name
			version = project.mod_version
			from components.java
		}
	}
}

modrinth {
	token = hasProperty("modrinthToken") ? modrinthToken : null
	projectId = "halplibe" // This can be the project ID or the slug. Either will work!
	versionName = "HalpLibe ${project.mod_version}"
	versionNumber = project.mod_version // You don't need to set this manually. Will fail if Modrinth has this version already
	versionType = "release" // This is the default -- can also be `beta` or `alpha`
	uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
	additionalFiles = [remapSourcesJar]
	gameVersions = ["b1.7.3"] // Must be an array, even with only one version
	loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
	changelog = Files.readString(java.nio.file.Path.of("${rootProject.projectDir}/CHANGELOG.md"))
	dependencies { // A special DSL for creating dependencies
		// scope.type
		// The scope can be `required`, `optional`, `incompatible`, or `embedded`
		// The type can either be `project` or `version`
		//required.project "fabric-api" // Creates a new required dependency on Fabric API
		//optional.version "sodium", "mc1.19.3-0.4.8" // Creates a new optional dependency on this specific version of Sodium
	}
}

import org.kohsuke.github.GHReleaseBuilder
import org.kohsuke.github.GitHub

import java.nio.file.Files

tasks.register('github') {
	doLast {
		def github = GitHub.connectUsingOAuth(turnipLabsGithubToken as String)
		def repository = github.getRepository("Turnip-Labs/bta-halplibe")

		def releaseBuilder = new GHReleaseBuilder(repository, version as String)
		releaseBuilder.name("HalpLibe $version")
		releaseBuilder.body(Files.readString(java.nio.file.Path.of("${rootProject.projectDir}/CHANGELOG.md")))
		releaseBuilder.commitish("7.3")

		def release = releaseBuilder.create()
		release.uploadAsset(project.file(remapJar.getArchiveFile().get().getAsFile()),"application/java-archive")
		release.uploadAsset(project.file(remapSourcesJar.getArchiveFile().get().getAsFile()),"application/java-archive")
	}
}